/*
 * Copyright 2019 Bytabit AB
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://dl.bintray.com/palantir/releases"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.11'
        classpath 'com.palantir:gradle-gitsemver:0.6.0'
        classpath 'io.franzbecker:gradle-lombok:1.14'
    }
}

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'java'
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'gitsemver'

lombok {
    version = '1.18.4'
    sha256 = ""
}

repositories {
    jcenter()
    maven { url 'http://nexus.gluonhq.com/nexus/content/repositories/releases' }
    mavenLocal()
    mavenCentral()
}

mainClassName = 'com.bytabit.mobile.BytabitMobile'

def semver = semverVersion().toString()
def net = 'test'
def config = 'default'

if (project.hasProperty('btcNetwork')) {
    net = btcNetwork
}

if (project.hasProperty('configName')) {
    config = configName
}

def url = "https://" + net + ".bytabit.net"

if (project.hasProperty('baseUrl')) {
    url = baseUrl
}

println net
println config

processResources {
    filesMatching("*.properties") {
        filter ReplaceTokens, tokens: [VERSION: semver, BTCNETWORK: net, CONFIGNAME: config, BASEURL: url]
    }

    outputs.upToDateWhen { false }
}

dependencies {
    compile 'com.gluonhq:charm:5.0.1'
    compile 'com.airhacks:afterburner.mfx:1.6.3'
    compile "org.bitcoinj:bitcoinj-core:0.14.7"
    compile 'io.reactivex.rxjava2:rxjavafx:2.2.0'
    compile 'org.slf4j:slf4j-api:1.7.21'

    compile 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-gson:2.4.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    desktopRuntime "ch.qos.logback:logback-classic:1.1.9"
    desktopRuntime "ch.qos.logback:logback-core:1.1.9"

    androidRuntime 'com.github.tony19:logback-android-core:1.1.1-6'
    androidRuntime('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    testImplementation "ch.qos.logback:logback-classic:1.1.9"
    testImplementation "ch.qos.logback:logback-core:1.1.9"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
}

jfxmobile {
    downConfig {
        version = '3.8.0'
        plugins 'display', 'lifecycle', 'statusbar', 'storage', 'settings', 'share', 'device'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
        applicationPackage = 'com.bytabit.mobile'
        compileSdkVersion = 26

        dexOptions {
            incremental true
            javaMaxHeapSize "4g"
        }
        packagingOptions {
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/NOTICE'
        }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.bytabit.**.*',
                'com.gluonhq.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*',
                'ch.qos.logback.**.*',
                'com.google.**.*',
                'com.madgag.spongycastle.**.*',
                'io.reactivex.rxjava2.**.*',
                'org.bitcoinj.**.*',
                'org.reactivestreams.**.*',
                'org.slf4j.**.*'
        ]
    }
}