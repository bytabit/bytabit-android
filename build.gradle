import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://dl.bintray.com/palantir/releases"
        }
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.2.0'
        classpath 'com.palantir:gradle-gitsemver:0.6.0'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'gitsemver'

repositories {
    jcenter()
    maven { url 'http://nexus.gluonhq.com/nexus/content/repositories/releases' }
    maven { url "https://jitpack.io" }
}

mainClassName = 'com.bytabit.mobile.BytabitMobile'

def semver = semverVersion().toString()
def net = 'test'
def config = 'default'

if (project.hasProperty('btcNetwork')) {
    net = btcNetwork
}

if (project.hasProperty('configName')) {
    config = configName
}

println net
println config

processResources {
    filesMatching("*.properties") {
        filter ReplaceTokens, tokens: [VERSION: semver, BTCNETWORK: net, CONFIGNAME: config]
    }
}

dependencies {
    compile 'com.gluonhq:charm:4.3.0'
    compile 'com.airhacks:afterburner.mfx:1.6.3'
    compile "org.bitcoinj:bitcoinj-core:0.14.3"
    compile "com.madgag.spongycastle:prov:1.51.0.0"
    compile "joda-time:joda-time:2.9.1"
    compile 'io.reactivex:rxjavafx:1.0.0'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'com.fasterxml.jackson.jr:jackson-jr-retrofit2:2.8.7'
    
    compile 'com.github.kenglxn.QRGen:javase:2.2.0'
    desktopCompile "ch.qos.logback:logback-classic:1.1.9"
    desktopCompile "ch.qos.logback:logback-core:1.1.9"

    //androidCompile 'com.github.kenglxn.QRGen:android:2.2.0'
    androidCompile 'com.github.tony19:logback-android-core:1.1.1-6'
    androidCompile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    desktopRuntime "com.gluonhq:charm-desktop:3.0.0"
    androidRuntime "com.gluonhq:charm-android:3.0.0"
    iosRuntime "com.gluonhq:charm-ios:3.0.0"

    testCompile 'junit:junit:4.12'
}

jfxmobile {
    downConfig {
        version = '3.2.0'
        plugins 'display', 'lifecycle', 'statusbar', 'storage', 'settings'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
        dexOptions {
            incremental true
            javaMaxHeapSize "4g"
        }
        packagingOptions {
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/NOTICE'
        }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.bytabit.mobile.**.*',
                'com.gluonhq.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}
