import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://dl.bintray.com/palantir/releases"
        }
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.2.0'
        classpath 'com.palantir:gradle-gitsemver:0.6.0'
    }
}

apply plugin: 'org.javafxports.jfxmobile'
apply plugin: 'gitsemver'

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
}

mainClassName = 'com.bytabit.ft.FiatTraderMobile'

def semver = semverVersion().toString()
def btcNetwork = 'test'
def configName = 'default'

if (project.hasProperty('regtest')) {
    btcNetwork = 'regtest'
}

if (project.hasProperty('trader2')) {
    configName = 'trader2'
}

processResources {
    filesMatching("*.properties") {
        filter ReplaceTokens, tokens: [VERSION: semver, BTCNETWORK: btcNetwork, CONFIGNAME: configName]
    }
}

dependencies {
    compile 'com.gluonhq:charm:4.3.0'
    compile 'com.airhacks:afterburner.fx:1.6.0'
    compile "org.bitcoinj:bitcoinj-core:0.14.3"
    compile "com.madgag.spongycastle:prov:1.51.0.0"
    compile "joda-time:joda-time:2.9.1"
    compile 'io.reactivex:rxjavafx:1.0.0'
    compile "org.slf4j:slf4j-jdk14:1.7.21"

    desktopRuntime "com.gluonhq:charm-desktop:3.0.0"
    androidRuntime "com.gluonhq:charm-android:3.0.0"
    iosRuntime "com.gluonhq:charm-ios:3.0.0"
}

jfxmobile {
    downConfig {
        version = '3.2.0'
        plugins 'display', 'lifecycle', 'statusbar', 'storage'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.bytabit.ft.**.*',
                'com.gluonhq.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}
